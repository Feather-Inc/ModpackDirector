plugins {
    id 'java-library'
}

group 'com.juanmuscaria'
version '1.0'

dependencies {
    implementation group: "com.fasterxml.jackson.core", name: "jackson-core", version: "2.13.5"
    implementation group: "com.fasterxml.jackson.core", name: "jackson-annotations", version: "2.13.5"
    implementation group: "com.fasterxml.jackson.core", name: "jackson-databind", version: "2.13.5"

    implementation 'com.jetbrains.intellij.java:java-gui-forms-rt:233.13135.104'
    implementation 'com.formdev:flatlaf:3.4.1'
}

allprojects {
    group = rootProject.group
    version = rootProject.version

    pluginManager.withPlugin('java') {
        repositories {
            mavenCentral()
            maven {
                name = 'intellij-repository'
                url = 'https://www.jetbrains.com/intellij-repository/releases/'
            }
        }

        configurations {
            compileOnly {
                extendsFrom annotationProcessor
            }
        }

        // Jabel stuff, compile with new java features while targeting java 8
        dependencies {
            annotationProcessor 'com.github.bsideup.jabel:jabel-javac-plugin:0.4.2'
            annotationProcessor 'org.projectlombok:lombok:1.18.30'
        }

        configure([tasks.compileJava]) {
            sourceCompatibility = 17 // for the IDE support
            options.release = 8

            javaCompiler = javaToolchains.compilerFor {
                languageVersion = JavaLanguageVersion.of(17)
            }
        }

        compileTestJava {
            sourceCompatibility = targetCompatibility = 8
        }

        test {
            javaLauncher = javaToolchains.launcherFor {
                languageVersion = JavaLanguageVersion.of(8)
            }
        }

    }

    pluginManager.withPlugin('application') {
        run {
            javaLauncher = javaToolchains.launcherFor {
                languageVersion = JavaLanguageVersion.of(8)
            }
        }
    }
}
